# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]
pkgname=vdr-softhdvaapi
pkgver=1.1.0.r10.g8682ab0
_gitver=5cd68b6eed98bc2ec2320798f7f6f89c15c487bf
_vdrapi=2.4.1
pkgrel=3
pkgdesc="VDR output plugin with CUDA and Opengl"
url="https://github.com/jojo61/vdr-plugin-softhdcuvid"
arch=('x86_64')
license=('AGPL3')
depends=('ffmpeg' 'freeglut' 'glew' 'mesa' "vdr-api=${_vdrapi}" 'xcb-util-wm' 'xorg-server' 'libplacebo>=1.7.0')
optdepends=('vdr-xorg: Recommended way to start X.org server together with VDR')
makedepends=('git' 'glm' 'glu' 'vulkan-headers')
_plugname=${pkgname//vdr-/}
source=("vdr-plugin-${_plugname}::git+https://github.com/jojo61/vdr-plugin-softhdcuvid#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
sha512sums=('SKIP'
         '4c5881d333d5b6e978185e052ec52d9a70c7617eb15a4dabda47546adb0110145de7591846906e3912122f6a2ec04b4bf49e8cf762058d1d8f595a3922655a28')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"

  # Disable OSS. Arch Linux doesn't ship OSS
  sed -i '/OSS /d' Makefile

  # Enable libplacebo, diable CUVID and enable VAAPI
  sed -i -e 's/LIBPLACEBO=0/LIBPLACEBO=1/' -e 's/#VAAPI=1/VAAPI=1/' -e 's/CUVID=1/CUVID=0/' Makefile

}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}

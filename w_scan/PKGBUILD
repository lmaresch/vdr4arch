# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=w_scan
pkgver=20170107
pkgrel=1
pkgdesc="Small channel scan tool for vdr"
url="https://www.gen2vdr.de/wirbel/w_scan/index2.html"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' 'glibc')
#gcc-libs is somehow needed on arm architecture
source=("https://www.gen2vdr.de/wirbel/w_scan/$pkgname-$pkgver.tar.bz2"
        "$pkgname-gcc10-fix.patch")
md5sums=('c7f9adf92d46b8da5391be80be6fbd72'
         '1446179a56ec5ed53f714424f12cc552')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "$srcdir/$pkgname-gcc10-fix.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
